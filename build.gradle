plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.graalvm.buildtools.native' version '0.9.17'
	id 'jacoco'
	id 'org.liquibase.gradle' version '2.1.1'
	id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'org.kg'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

testSets {
	integrationTest
}

liquibase {
	activities {
		doFirst {
			if (!project.hasProperty('runList')) {
				project.ext.runList = 'local'
			}
		}
		local {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			defaultSchemaName 'boardgames-api'
			url 'jdbc:postgresql://localhost:5432/postgres_shared?currentSchema=boardgames-api'
			username 'postgres'
			password 'postgres'
			driver 'org.postgresql.Driver'
			classpath "$rootDir"
		}
		prod {
			changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			defaultSchemaName 'boardgames-api'
			url System.getenv('JDBC_DATABASE_URL')
			username System.getenv('JDBC_DATABASE_USERNAME')
			password System.getenv('JDBC_DATABASE_PASSWORD')
			driver 'org.postgresql.Driver'
			classpath "$rootDir"
		}
	}
	runList = project.ext.runList
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

dependencies {


	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.apache.commons:commons-lang3'


	runtimeOnly 'org.postgresql:postgresql'

	// Liquibase
	liquibaseRuntime 'org.liquibase:liquibase-core:4.4.3'
	liquibaseRuntime 'org.yaml:snakeyaml:1.32'
	liquibaseRuntime 'info.picocli:picocli:4.6.3'
	liquibaseRuntime 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestImplementation 'com.h2database:h2'
}

tasks.withType(Test) {
	useJUnitPlatform()
}

task local {
	bootRun { systemProperty 'spring.profiles.active', 'local' }
}

task deleteLiquibaseUpdateDir(type: Delete) {
	delete "${project.buildDir}/native/agent-output/update"
}

bootRun.mustRunAfter local

update.dependsOn deleteLiquibaseUpdateDir

check.dependsOn integrationTest
integrationTest.mustRunAfter test
